grammar dk.sdu.mmmi.mdsd.Math with org.eclipse.xtext.common.Terminals

generate math "http://www.sdu.dk/mmmi/mdsd/Math"

Program:
	"program" name=ID
	externalDeclarations+=ExternalDeclaration*
	exp=MathExp
;

ExternalDeclaration:
	"external" name=ID '('(typeParameters+=ID (',' typeParameters+=ID)*)?')'
;

MathExp:
	{MathExp} variables += VarBinding*
;

VarBinding:
	'var' name=ID '=' expression=Exp
;

Exp returns Expression:
	Factor (( {Plus.left=current} '+' | {Minus.left=current}  '-' ) right=Factor)*
;

Factor returns Expression:
	Primary (({Mult.left=current} '*' | {Div.left=current}  '/'  ) right=Primary)*
;

Primary returns Expression:
	{MathNumber} value=INT  | Parenthesis | ExternalCall |  VariableUse | LetBinding
;

Parenthesis:
	'(' exp=Exp ')'
;

ExternalCall:
	function=[ExternalDeclaration] '('(expressions+=Exp (',' expressions+=Exp)*)?')'
;

LetBinding:
	'let' name=ID '=' binding=Exp 'in' body=Exp 'end'
;

Binding:
	VarBinding | LetBinding
;

VariableUse:
	ref = [Binding]
;
